use program::posix::ast;
use program::posix::lex;
use program::ast::Interpreter;

grammar<'input>(text: &'input str);

extern {
    type Location = usize;
    type Error = lex::Error;

    enum lex::Tok<'input> {
        " "    => lex::Tok::Space,
        "\t"   => lex::Tok::Tab,
        "\n"   => lex::Tok::Linefeed,
        ";"    => lex::Tok::Linefeed,
        "&"    => lex::Tok::Amper,
        "{"    => lex::Tok::RBrace,
        "}"    => lex::Tok::LBrace,
        "("    => lex::Tok::RParen,
        ")"    => lex::Tok::LParen,
        "`"    => lex::Tok::Backtick,
        "!"    => lex::Tok::Bang,
        "|"    => lex::Tok::Pipe,
        "$"    => lex::Tok::Dollar,
        "="    => lex::Tok::Equals,
        "\\"   => lex::Tok::Backslack,
        "\""   => lex::Tok::DoubleQuote,
        "'"    => lex::Tok::SingleQuote,
        ">"    => lex::Tok::RCaret,
        "<"    => lex::Tok::LCaret,
        "&&"   => lex::Tok::And,
        "||"   => lex::Tok::Or,
        "WORD" => lex::Tok::Word(<&'input str>),
        "#!"   => lex::Tok::Shebang,
    }
}

pub Program: ast::Program = {
    <p: Program> "\n" <c: Complete> => {
        println!("hit");
        p.append(&c)
    },
    Complete => <>,
}

Complete: ast::Program = {
    <l: List> ";" => l,
    <l: List> "&" => {
        let bg = ast::Command::Background(box l);
        ast::Program(vec![box bg])
    },
    List => <>,
}

List: ast::Program = {
    <l: List> ";" <cs: Commands> => {
        l.push(&cs)
    },
    <l: List> "&" <cs: Commands> => {
        let bg = ast::Command::Background(box l);
        ast::Program(vec![box bg, box cs])
    },
    Commands => {
        ast::Program(vec![box <>])
    },
}

Compound: ast::Program = {
    <cs: Commands> ";" <c: Compound> => {
        c.insert(&cs)
    },
    <cs: Commands> ";" => {
        ast::Program(vec![box cs])
    },
}

Commands: ast::Command = {
    // TODO #15: Hopefully in fixing #8 and #10 this can play nicely.
    // NOTE: This can be successfully complied, but will break a doc tests.
    "{" "#!" <i: "WORD"+> ";" <t: "WORD"*> "}" => {
        let i = i.join(" ");
        let t = t.join(" ");
        let b = ast::BridgedProgram(Interpreter::Other(i), t);
        ast::Command::Bridgeshell(box b)
    },
    "(" <p: Program> ")" => {
        ast::Command::Subshell(box p)
    },
    "{" <c: Compound> "}" => {
        ast::Command::Compound(box c)
    },
    <cs: Commands> "&&" <p: Pipeline> => {
        ast::Command::And(box cs, box p)
    },
    <cs: Commands> "||" <p: Pipeline> => {
        ast::Command::Or(box cs, box p)
    },
    Pipeline => <>,
}

Pipeline: ast::Command = {
    "!" <ps: PipelineSeq> => {
        ast::Command::Not(box ps)
    },
    <ps: PipelineSeq> => ps,
}

PipelineSeq: ast::Command = {
    <ps: PipelineSeq> "|" "\n"* <c: Command> => {
        ast::Command::Pipeline(box ps, box c)
    },
    <c: Command> => c,
}

Command: ast::Command = {
    <ws: ("WORD" " ")+> <w: "WORD"> => {
        ast::Command::Simple(ws.iter().map(|(w, _)| {
            ast::Word(w.to_string())
        }).collect())
    },
    "WORD" => {
        ast::Command::Simple(vec![ast::Word(<>.to_string())])
    },
};
